buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
sourceCompatibility = 1.8
targetCompatibility = 1.8

loadProperties()

version = config.minecraft_version + "-" + config.mod_version + "." + config.build_number
group= "com.thexfactor117.losteclipse"
archivesBaseName = "Lost-Eclipse"

def loadProperties() {
    ext.configFile = file "build.properties"

    configFile.withReader {
        def prop = new Properties()
        prop.load(it)
        project.ext.config = new ConfigSlurper().parse prop
    }
}

minecraft {
    version = config.forge_version
    runDir = "run"
    mappings = config.mappings
    makeObfSourceJar = false
    replace "@VERSION@", config.mod_version + "." + config.build_number
    replace "@MVERSION@", version
}

dependencies {
	compile files (
	"libs/Levels-" + config.levels_version + ".jar"
	)
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':config.mod_version + "." + config.build_number, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
